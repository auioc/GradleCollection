apply plugin: 'maven-publish'

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            artifact jar
            artifact deobfJar {
                classifier 'deobf'
            }
            artifact sourcesJar {
                classifier 'sources'
            }
            artifact forgelibJar {
                classifier 'forgelib'
            }
        }
    }
    repositories {
        maven {
            name = 'McmodsRepo'
            url "file://${project.projectDir}/mcmodsrepo"
        }
    }
}
if (project.hasProperty('github_repo')) {
    publishing.repositories {
        maven {
            name = 'GitHubPackages'
            url = uri("https://maven.pkg.github.com/${github_repo}")
            credentials {
                username = System.getenv("GITHUB_ACTOR") ? System.getenv("GITHUB_ACTOR") : System.getenv("GITHUB_USERNAME")
                password = System.getenv('GITHUB_TOKEN')
            }
        }
    }
}
if (project.group == 'org.auioc.mcmod') {
    publishing.repositories {
        maven {
            name = 'AuiocMavenRepo'
            url = uri("https://repo.auioc.com/maven/releases")
            credentials {
                username = System.getenv('AUIOC_MAVEN_USERNAME')
                password = System.getenv('AUIOC_MAVEN_TOKEN')
            }
        }
    }
}
