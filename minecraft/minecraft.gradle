project.minecraft {
    mappings channel: "${mappings_channel}", version: "${mappings_version}"
    runs {
        client {}
        server {}
    }
}

if (project.hasProperty('datagen_enabled') && "${project.datagen_enabled}".toBoolean()) {
    project.minecraft { runs { data {} } }
}

project.minecraft.runs.each {
    it.setWorkingDirectory(project.file('run').canonicalPath)
    it.properties.put('file.encoding', 'UTF-8')
    it.properties.put('user.language', 'en')
    it.properties.put('log4j.configurationFile', 'log4j2.xml')
    if (project.hasProperty('mixinEnabled') && project.mixinEnabled && it.getName() != 'data') {
        it.properties.put('mixin.debug', 'true')
    }
    if (project.hasProperty('mixin_remap') && "${project.mixin_remap}".toBoolean()) {
        it.properties.put('mixin.env.remapRefMap', 'true')
        it.properties.put('mixin.env.refMapRemappingFile', "${buildDir}/createSrgToMcp/output.srg")
    }
    it.mods.maybeCreate("${project.modId}")
    it.mods.each {
        if (it.getName() == "${project.modId}" && !it.hasResources()) {
            it.source(project.sourceSets.main)
        }
    }
    if (it.getName() == 'data') {
        project.sourceSets.main.resources { srcDir 'src/generated/resources' }
        it.setForceExit(false) // https://forums.minecraftforge.net/topic/87940-gradlew-rundata-task-fail/
        if (it.args.size() == 0) {
            it.args('--mod', "${project.modId}", '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/'))
        }
    }
}

project.sourceSets.remove(sourceSets.test)

project.dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
}
